#!/usr/bin/perl
use strict;
use warnings;
use feature qw(say);
use autodie;
use utf8;
binmode STDOUT, ':encoding(UTF-8)';
use Storable;
my $Storable_file = 'CRE_info_hash';

my %CRE = %{ retrieve $Storable_file };

open my $dot_fh, '>', 'more-than-one-interlocutor.dot';
say $dot_fh "digraph G {\n\toverlap = false\n";

my %interlocutor;
my %interlocutor_count;
for my $key ( keys %CRE ) {
    my $interlocutor = $CRE{$key}{'interlocutor'};
    next unless defined $interlocutor;
    $interlocutor =~ s/"/\\"/xmsg;
    my @interlocutors = split /,\s+/xms, $interlocutor;
    next if @interlocutors == 1; ## Boring
    for (@interlocutors) {
        push @{ $interlocutor{$_} }, $key;
        $interlocutor_count{$_}++;
    }
}

for my $interlocutor ( sort { $interlocutor_count{$b} <=> $interlocutor_count{$a} } keys %interlocutor_count ) {
    #next if $interlocutor_count{$interlocutor} == 1;
    #say "$interlocutor: $interlocutor_count{$interlocutor}  @{$interlocutor{$interlocutor}}";
    for ( @{ $interlocutor{$interlocutor} } ) {
        say {$dot_fh} qq("$interlocutor" -> "$CRE{$_}{'title'}";);
    }
}

say $dot_fh '}';
close $dot_fh;
